-- Digital Lock NuSMV module

MODULE main
VAR
    state           : {start_0, left_12, right_21, open_15};        -- 4 states in locking process, 0,12,21,15
    position        : 0..31;        -- from positions 0-31 (32-39 ignored)
    upReg           : boolean;      -- counter-clockwise movement of dial 
    downReg         : boolean;      -- clockwise movement of dial 
    open            : boolean;      -- signifies if lock is open
    up              : boolean;      -- signals
    down            : boolean;      -- signals

 
ASSIGN
    init (position) := 0;
    init (state)    := start_0;
    init (upReg)    := FALSE;
    init (downReg)  := FALSE;
    init (open)     := FALSE;

    next (open)     := 
        case 
            state = open_15 : TRUE;  -- if we're in the last state, the lock is open
            TRUE : FALSE;            -- otherwise it's closed
        esac;

    
    -- Defining when we're turning left or right
    next (upReg)        := up & !down;
    next (downReg)      := !up & down;
      
    -- states including input values and positions 
    next (state)    := 
        case
            (state = start_0) & (upReg) & (position = 12)          : left_12;
            (state = left_12) & (downReg) & (position  = 21)       : right_21;
            (state = right_21) & (upReg) & (position    = 15)      : open_15;
            (state = open_15) & (upReg | downReg)                  : start_0; 
            TRUE : state;       --default
        esac;

    next (position) :=
        case 
            (up & !down) & position = 0  : 31; 
            (!up & down) & position = 31  : 0;
            up & !down  : position - 1; 
            !up & down  : position + 1; 
            -- else it stays the same
            TRUE : position;
        esac;

 
-- Test property - should fail
SPEC AG (open & !open) 
 
-- This property states that if the lock is open and it is not interrupted, it can remain open indefinitely.
SPEC AG (open & !up & !down) -> (open)

-- This property states that a transition to state two can only happen under the required circumstances.
SPEC AG (state=left_12 & !open & down & !up & position=21) -> (state = right_21)

-- This property states that if the lock is open and it is interrupted the the lock will close.
SPEC AG((open & up & !down) | (open & down & !up)) -> AX(!open)

-- This property states that it is always possible for the lock to be closed.
SPEC AG(EF(!open))

-- This property states that the lock will not open by counting down to 15.
SPEC AG(down & position=15) -> (!open)
